plugins {
    id 'fabric-loom' version '0.5.11'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    jcenter()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    include(modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version)))

    include(modApi("me.shedaniel.cloth:config-2:4.6.0") {
        exclude module:"fabric-api"
    })
    include(modImplementation("me.sargunvohra.mcmods:autoconfig1u:3.2.0-unstable"))

    modImplementation("io.github.prospector:modmenu:1.14.6+build.31") {
        exclude module:"fabric-api"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

tasks.remapJar {
    afterEvaluate {
        addNestedDependencies = false
        archiveClassifier.set("slim")
    }
}

task remapStandaloneJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}-fat.jar"
        addNestedDependencies = true
    }
}

tasks.build.dependsOn remapStandaloneJar

curseforge {
    // Stored in ~/.gradle/gradle.properties
    if (project.hasProperty('curseApiKey'))
        apiKey = project.curseApiKey
    else if (System.getenv("CURSE_API_KEY") != null)
        apiKey = System.getenv("CURSE_API_KEY")
    else
        println 'No CurseForge API key found, \'curseforge\' tasks will not work'

    project {
        id = project.curseforge_id
        releaseType = 'release'
        addGameVersion project.minecraft_version
        addGameVersion 'Fabric'
        setChangelog 'For the changelog, please see https://github.com/Scotsguy/now-playing/releases'
        mainArtifact(remapStandaloneJar) {
            displayName = "v$project.version"
            relations {
                embeddedLibrary 'fabric-api'
                embeddedLibrary 'cloth-config'
                embeddedLibrary 'auto-config-updated-api'
            }
        }
        addArtifact remapJar
    }
    options {
        forgeGradleIntegration = false
    }
}


// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
